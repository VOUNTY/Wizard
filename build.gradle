plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group project.property("deploy_group")
version project.property("deploy_version")

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.mindrot:jbcrypt:${bcrypt_version}"
    implementation "com.google.code.gson:gson:${gson_version}"
    implementation "org.jline:jline:${jline_version}"
    implementation "com.sparkjava:spark-core:${spark_version}"
    implementation "org.apache.commons:commons-lang3:${commons_lang_version}"
    implementation "ch.qos.logback:logback-classic:${logback_version}"

    compileOnly "org.projectlombok:lombok:${lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${lombok_version}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'net.vounty.wizard.Bootstrap',
                'Implementation-Version': project.version,
        )
    }

    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.property("deploy_artifact")
            version = project.version

            from components.java
            artifact sourcesJar
        }
    }

    repositories {
        maven {
            url = "${deploy_url}${project.version.endsWith("RELEASE") ? 'releases' : 'snapshots'}"
            credentials {
                username = project.hasProperty("deployName") ? project.property("deployName") : ""
                password = project.hasProperty("deployPassword") ? project.property("deployPassword") : ""
            }
        }
    }
}